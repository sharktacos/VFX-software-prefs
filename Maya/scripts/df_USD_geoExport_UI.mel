//---------------------------------------------------------
/*
 Export geo to USD with support for:
 - Proxy/render display purpose scope names detected from selected heirarchy.
 - Reference geometry (.usdc) into payload file (.usda) which is payloaded into Asset file
 - Custom metadata with originating Maya file “exported_from”
 
 Usage: Select the root prim to export. Confirm proxy and render groups in file dialog. Enter path and filename for root asset. Export.
 Will create a root asset file and corresponding geom and payload files.
 
 call with:
 source df_USD_geoExport_UI;
 mayaUsd_exportGeo();
 
 v3 (c) Derek Flood, 2023
 
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
# FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
# DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
# SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
# CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
# OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/
//---------------------------------------------------------

proc setLatestLoadStageFolder( string $sceneFolder )
{
    optionVar -stringValue mayaUsd_LatestLoadStageFolder $sceneFolder;
}

proc string getLatestLoadStageFolder()
{
    string $sceneFolder;

    // First check if we've saved a location in the option var
    if (`optionVar -exists mayaUsd_LatestLoadStageFolder`)
    {
        $sceneFolder = `optionVar -q mayaUsd_LatestLoadStageFolder`;
    }

    // Then check if there is a current Maya scene, if so choose that as
    // a starting point.
    if ("" == $sceneFolder)
    {
        $sceneFolder = dirname(`file -q -sceneName`);
    }

    // If we are really starting from scratch then just go with the 
    // current workspace location for scenes.
    if ("" == $sceneFolder)
    {
        string $workspaceLocation = `workspace -q -fn`;
        string $scenesFolder = `workspace -q -fileRuleEntry "scene"`;
        $sceneFolder = $workspaceLocation + "/" + $scenesFolder;
    }

    return $sceneFolder;
}

proc setOptionVars()
{
    //Get the child group names under selected to populate purpose fields
    $sel = `ls -sl`;
    //$dag_root = `substitute "\|" $sel[0] "x"`;
    $dag_root = substituteAllString($sel[0], "|", "");
    $geoGrpPath = `listRelatives -path $sel`;
    $find_groups = `listRelatives $geoGrpPath`;
    $found_render = $find_groups[0];
    $found_proxy = $find_groups[1];

    if (!`optionVar -exists optVar_root`) {
        optionVar -stringValue optVar_root $dag_root;
    }
    
    if (!`optionVar -exists optVar_render`) {
        optionVar -stringValue optVar_render $found_render;
    }

    if (!`optionVar -exists optVar_proxy`) {
        optionVar -stringValue optVar_proxy $found_proxy;
    }
}

// creates the options of the File dialog
global proc string df_UISetup(string $parent)
{
    setParent $parent;
    string $layout = `scrollLayout -childResizable true`;

    setParent $layout;
    $title = "Render and Proxy Purpose Scope Names (Detected from selected hierarchy)";
    $frame = `frameLayout -label $title -collapsable false`;
    text -align left -label "";
    textFieldGrp -ed 0 -bgc 0.15 0.15 0.15 -label "Root Prim:" -ann "Derived from selected node."  root_field;
    text -align left -label "           	geo/";
    textFieldGrp -ed 0 -bgc 0.6 0.6 0.6 -label "Render Purpose:" -ann "Derived from children of selected root node."  render_field;
    textFieldGrp -ed 0 -bgc 0.6 0.6 0.6 -label "Proxy Purpose:" -ann "Derived from children of selected root node."  proxy_field;
    
    
    return $layout;
}

// UIInit
global proc df_UIInit(string $parent, string $filterType)
{
    setOptionVars();

    string $xpath = `optionVar -q optVar_root`;
    string $rpath = `optionVar -q optVar_render`;
    string $ppath = `optionVar -q optVar_proxy`;

    setParent $parent;
    textFieldGrp -e -text $xpath root_field;
    textFieldGrp -e -text $rpath render_field;
    textFieldGrp -e -text $ppath proxy_field;

}

global proc df_UICommit(string $parent)
{
    setParent $parent;

    // fetch values
    optionVar -stringValue optVar_root
        (`textFieldGrp -q -text  root_field`);
    optionVar -stringValue optVar_render
        (`textFieldGrp -q -text  render_field`);
    optionVar -stringValue optVar_proxy
        (`textFieldGrp -q -text  proxy_field`);
}

global proc mayaUsd_exportGeo()
{

    optionVar -remove optVar_root;
    optionVar -remove optVar_render;
    optionVar -remove optVar_proxy;

    $sel = `ls -sl`;
    $isSel = `size($sel)`;
    $workspace = `workspace -rootDirectory -query`;
    string $startFolder = getLatestLoadStageFolder();
    $geoGrp = `listRelatives $sel`;
    $geoGrpPath = `listRelatives -path $sel`;
    $GroupCount = `listRelatives -path $geoGrpPath`;
    $justTwo = `size $GroupCount`;

    if ($isSel){
        
        if ($geoGrp[0] != "geo"){
            confirmDialog -message "Missing \"geo\" group. Structure must be \n\nroot/ (asset name)\n   geo/\n      render/ \n      proxy/\n\nNote that \"geo\" must be the exact name for the geometry scope. The render and proxy purpose scopes can be named as desired based on your studio naming conventions. The root prim is typically given the name of the asset. For example \"fruitBowl\" " -button "Got it";
            return;
        }

        if ($justTwo != 2) {
            confirmDialog -message "Just two child groups under \"geo\" please. Render and Proxy, in that order" -button "Sorry";
            return;
        }

        string $filepath[] = `fileDialog2 
                      -dialogStyle 2
                      -fileMode 0
                      -caption "Export selected to USD with geometry payloaded into asset file" 
                      -fileFilter "USD Export (*.usd *.usdc *.usda)" 
                      -okCaption "Export" 
                      -dir $startFolder 
                      -optionsUICreate "df_UISetup" 
                      -optionsUIInit "df_UIInit" 
                      -optionsUICommit "df_UICommit"`;

        $render_value = `optionVar -q optVar_render`; 
        $proxy_value = `optionVar -q optVar_proxy`; 
        $fileName = $filepath[0];
        
        if (size($fileName) > 0){
            setLatestLoadStageFolder( dirname($filepath[0]) );
            python("import df_USD_geoExport; df_USD_geoExport.main('" + $fileName + "', '" + $render_value + "', '" + $proxy_value + "')");
        }
        


    } else {
        confirmDialog -message "Nothing Selected. Select the root node of your asset." -button "Derp";
    }
}

//mayaUsd_exportGeo()

